buildscript {
    repositories {
        //mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-docs:${project.ext.properties.grailsDocsVersion ?: grailsVersion}"
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
        classpath "io.micronaut.docs:micronaut-docs-asciidoc-extensions:1.0.0.M1"
        classpath "io.micronaut.docs:micronaut-docs-gradle-plugins:1.0.0.M1"
    }
}

def DOCUMENTATION_GROUP = 'Documentation'

def commonGithubOrg = 'grails'
def commonGithubSlug = 'grails-common-build'
def commonBranch = 'master'
def docResourcesDir = "${buildDir}/resources/${commonGithubSlug}-${commonBranch}/src/main/resources"

subprojects { subproject ->
    task moveConfigProps() {
        group 'documentation'
        doLast {
            ant.mkdir(dir:"${rootProject.buildDir}/config-props")
            ant.move(file: "${subproject.buildDir}/classes/java/main/META-INF/config-properties.adoc", tofile:"${rootProject.buildDir}/config-props/${subproject.name}-config-properties.adoc", failonerror:false, quiet:true)
        }
    }

    task javaDocAtReplacement(type: io.micronaut.docs.JavaDocAtValueReplacementTask, dependsOn: moveConfigProps) {
        adocFile = new File("${rootProject.buildDir}/config-props/${subproject.name}-config-properties.adoc")
        rootProjectDir = rootProject.projectDir
    }

    task replaceAtLink(dependsOn: moveConfigProps) {
        group 'documentation'

        doLast {
            File file = new File("${rootProject.buildDir}/config-props/${subproject.name}-config-properties.adoc")
            if ( file.exists()) {
                def atLinkReplacer = { String str ->
                    String newLine = str.substring(0, str.indexOf('{@link io.micronaut.'))
                    String sub = "api:"+str.substring(str.indexOf('{@link io.micronaut.') + '{@link io.micronaut.'.length())
                    newLine += sub.substring(0, sub.indexOf('}')) + '[]'
                    newLine += sub.substring(sub.indexOf('}') + '}'.length())
                    newLine
                }

                List<String> lines = file.readLines()
                List<String> outputLines = []
                for (String line : lines) {
                    String proccessedLine = line
                    while (proccessedLine.contains('{@link io.micronaut.')) {
                        proccessedLine = atLinkReplacer(proccessedLine)
                    }
                    outputLines << proccessedLine
                }
                file.text = outputLines.join('\n')
            }
        }
    }
    task processConfigProps(dependsOn: [replaceAtLink, javaDocAtReplacement]) {
        group 'documentation'
        ext {
            individualConfigPropsFolder = "${rootProject.buildDir}/generated/configurationProperties"
        }
        doFirst {
            if(!new File("${rootProject.buildDir}/generated").exists()) {
                new File("${rootProject.buildDir}/generated").mkdir()
            }
            if(!new File("${rootProject.buildDir}/generated/configurationProperties").exists()) {
                new File("${rootProject.buildDir}/generated/configurationProperties").mkdir()
            }
        }
        doLast {

            def f = new File("${rootProject.buildDir}/config-props/${subproject.name}-config-properties.adoc")
            if (f.exists()) {
                def lines = f.readLines()
                def separator = "<<<"
                List<String> accumulator = []
                String configurationPropertyName = ''
                for (String line : lines) {

                    if (line.startsWith('<a id="')) {
                        String sub = line.substring(line.indexOf('id="') + 'id="'.length())
                        sub = sub.substring(0, sub.indexOf('"'))
                        configurationPropertyName = sub
                    }
                    if (line == separator) {
                        File outputfile = new File("${individualConfigPropsFolder}/${configurationPropertyName}.adoc")
                        outputfile.createNewFile()
                        outputfile.text = accumulator.join('\n')
                        accumulator = []
                        configurationPropertyName = null
                    } else {
                        accumulator << line
                    }
                }
            }
        }
    }
}
task prepareDocResources {
    group = DOCUMENTATION_GROUP
    description = 'Downloads common documentation resoruces and unzips them to build folder'
    doLast {
        ant.mkdir(dir:buildDir)
        ant.get(src:"https://github.com/${commonGithubOrg}/${commonGithubSlug}/archive/${commonBranch}.zip", dest:"${buildDir}/resources.zip")
        ant.unzip(src:"${buildDir}/resources.zip", dest:"${buildDir}/resources")
    }
}

task copyLocalDocResources(type: Copy, dependsOn:prepareDocResources) {
    group = DOCUMENTATION_GROUP
    description = 'Copy local resources to build folder'
    mustRunAfter prepareDocResources
    from ('src/main/docs/resources')
    into docResourcesDir
}

configurations {
    documentation
}

dependencies {
    documentation("org.fusesource.jansi:jansi:1.14")
}

def cleanTask = project.tasks.findByName("clean")
if(cleanTask == null) {
    task clean(type: Delete) {
        delete(buildDir)
    }
}
else {
    cleanTask.doLast {
        ant.delete(dir:"build/docs")
    }
}

task javadoc(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation', overwrite:true) {
    destinationDir = file("$buildDir/docs/api")
    title = "$project.name $version API"
    options.author true
    List links = []
    for( p in project.properties ) {
        if(p.key.endsWith('api')) {
            links.add(p.value.toString())
        }
    }
    options.links links as String[]
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.addBooleanOption('notimestamp', true)

    subprojects.each { proj ->
        if(!proj.name != 'docs' && !proj.name.startsWith('examples') ) {

            proj.tasks.withType(Javadoc).each { javadocTask ->
                source += javadocTask.source
                classpath += javadocTask.classpath
                excludes += javadocTask.excludes
                includes += javadocTask.includes
            }
        }
    }
}

task cleanupPropertyReference {
    group = DOCUMENTATION_GROUP
    doLast {
        File f = new File( "${rootProject.buildDir}/docs/guide/configurationreference.html" )
        if(f.exists()) {
            f.delete()
        }
    }
}

task mergeConfigurationReference() {
    ext {
        inputFileName = "${rootProject.buildDir}/generated/propertyReference.adoc"
    }
    group = DOCUMENTATION_GROUP
    inputs.files( fileTree( "$project.buildDir/config-props" ) ).skipWhenEmpty()
    doLast {
        File outputFile = new File(inputFileName)
        outputFile.createNewFile()
        outputFile.withOutputStream { out ->
            List<File> files = new ArrayList<>(inputs.files.files).sort { File f -> f.name }
            for ( file in files ) {
                String header = "=== " + file.name.replace('.adoc', '').split('-').collect { token ->
                    "${token.charAt(0).toString().toUpperCase()}${token.substring(1)}"
                }.join(' ')
                file.withInputStream {
                    out << header << '\n' << it << '\n'
                }
            }
        }
    }
}
task publishConfigurationReference(dependsOn: mergeConfigurationReference) {
    ext {
        destinationFileName = "${rootProject.buildDir}/docs/guide/configurationreference.html"
    }
    doLast {
        String textPage = new File("${rootProject.projectDir}/src/main/docs/resources/style/page.html").text
        textPage = textPage.replace("@projectVersion@", projectVersion)
        textPage = textPage.replace("@pagetitle@", 'Configuration Reference | Micronaut')
        if (new File(mergeConfigurationReference.inputFileName).exists()) {
            String html = org.asciidoctor.Asciidoctor.Factory.create().render(new File(mergeConfigurationReference.inputFileName).text, [:])
            textPage = textPage.replace("@docscontent@", html)
            File configurationreference = new File(destinationFileName)
            configurationreference.createNewFile()
            configurationreference.text = textPage
        } else {
            logger.quiet "${mergeConfigurationReference.inputFileName} does not exist."
        }
    }
}

task cleanupGuideFiles(type: Delete) {
    group = DOCUMENTATION_GROUP
    delete fileTree("${rootProject.buildDir}/docs/guide") {
        include '*.html'
        exclude 'index.html'
        exclude publishConfigurationReference.destinationFileName
    }
    delete fileTree("${rootProject.buildDir}/docs/guide/pages") {
        include '*.html'
    }
}

task publishGuide(type: grails.doc.gradle.PublishGuide) {
    group = DOCUMENTATION_GROUP
    description = 'Generate Guide'
    dependsOn copyLocalDocResources

    targetDir = project.file("${buildDir}/docs")
    sourceRepo = "https://github.com/${githubSlug}/edit/${githubBranch}/src/main/docs"
    sourceDir = new File(projectDir, "src/main/docs")
    propertiesFiles = [ new File(rootProject.projectDir, "gradle.properties") ]
    asciidoc = true
    resourcesDir = project.file(docResourcesDir)
    properties = [
            'safe':'UNSAFE',
            'source-highlighter':'highlightjs',
            'version': project.version,
            'subtitle': project.projectDesc,
            'github': 'https://github.com/micronaut-projects/micronaut-core',
            'api': '../api',
            'sourceDir':rootProject.projectDir.absolutePath,
            'sourcedir':rootProject.projectDir.absolutePath,
            'includedir': "${rootProject.buildDir.absolutePath}/generated/",
            'javaee': 'https://docs.oracle.com/javaee/8/api/',
            'javase': 'https://docs.oracle.com/javase/8/docs/api/',
            'groovyapi': 'http://docs.groovy-lang.org/latest/html/gapi/',
            'grailsapi': 'http://docs.grails.org/latest/api/',
            'gormapi': 'http://gorm.grails.org/latest/api/',
            'springapi': 'https://docs.spring.io/spring/docs/current/javadoc-api/',
            'kafka-version': rootProject.properties['kafkaVersion']
    ]
    doLast {
        ant.move(file:"${project.buildDir}/docs/guide/single.html",
                tofile:"${project.buildDir}/docs/guide/index.html", overwrite:true)
        new File(project.buildDir, "docs/index.html").text = '''
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html lang="en">
<head>
<meta http-equiv="refresh" content="0; url=guide/index.html" />
</head>

</body>
</html>
'''
    }
}
task zipDocs(type:Zip) {
    group = DOCUMENTATION_GROUP
    baseName = "${project.name}-${project.version}"
    destinationDir = new File(project.buildDir, "distributions")
    from project.files("${buildDir}/docs")
}
//task assemble {
//    subprojects.each { subproject ->
//        if (subproject.tasks.findByName("assemble") != null) {
//            dependsOn subproject.tasks.findByName("assemble")
//        }
//    }
//}
task processConfigProps() {
    subprojects.each { subproject ->
        if (subproject.tasks.findByName("processConfigProps") != null) {
            dependsOn subproject.tasks.findByName("processConfigProps")
        }
    }
}
task docs(dependsOn: [assemble, javadoc, publishGuide, processConfigProps, publishConfigurationReference]) {
    group = DOCUMENTATION_GROUP
}
javadoc.mustRunAfter assemble
processConfigProps.mustRunAfter javadoc
processConfigProps.mustRunAfter assemble
publishGuide.mustRunAfter processConfigProps
publishConfigurationReference.mustRunAfter publishGuide
docs.finalizedBy zipDocs
